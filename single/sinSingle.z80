#define var_x 800Bh
#define var_y 800Fh
#define temp 8013h
sinSingle:
;sine(-pi/4<=x<pi/4)
;y=x*x
;a1=2^-3 * 11184804/2^23
;a2=2^-7 * 8946604/2^23
;a3=2^-13 * 13408017/2^23
;x(1-y(a1-y(a2-y*a3)))
;
  push hl
  push de
  push bc
  push af
  call +_
  pop af
  pop bc
  pop de
  pop hl
  ret
_:
;Need to verify that we are in fact in range
  push bc
  ld de,var_x
  ldi
  ldi
  ldi
  ldi
  ld hl,var_x
  ld d,h
  ld e,l
  ld bc,var_y
  call mulSingle
  ld h,b
  ld l,c
  ld de,sin_a3
  ld bc,temp
  call mulSingle
  ld hl,sin_a2
  ld d,b
  ld e,c
  call subSingle
  ld hl,var_y
  call mulSingle
  ld hl,sin_a1
  call subSingle
  ld hl,var_y
  call mulSingle
  ld hl,const_1
  call subSingle
  ld hl,var_x
  pop bc
  jp mulSingle
#undefine var_x
#undefine var_y
#undefine temp
